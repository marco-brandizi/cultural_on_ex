PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX socrata: <http://www.socrata.com/rdf/terms#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ods: <http://open-data-standards.github.com/2012/01/open-data-standards#>
PREFIX dcterm: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dsbase: <https://www.dati.lombardia.it/resource/>
PREFIX ds: <https://www.dati.lombardia.it/resource/_4mr7-hfsh/>
PREFIX usps: <http://www.w3.org/2000/10/swap/pim/usps#>

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cis: <http://dati.beniculturali.it/cis/>
PREFIX schema: <http://schema.org/>

PREFIX kr: <http://www.example.com/resources/>

CONSTRUCT {
  ?obj a cis:CulturalHeritageObject;
    rdfs:label ?title;
    schema:category ?category;
    dcterms:creator ?creator;
    dcterms:date ?period;
    dcterms:description ?descr;
    cis:url ?url;
    cis:hasAddress ?addr
  .

  ?addr a cis:Address;
    cis:adminUnitL1 ?region;
    cis:adminUnitL2 ?prov;
    cis:postName ?city;
    cis:postCode ?zip;
    cis:fullAddress ?addressLine;
    cis:hasGeometry ?loc
  .

  ?loc a cis:Geometry;
    cis:hasLat ?lat;
    cis:hasLong ?long
  .
}
WHERE
{
  ?obj ds:idbene ?id;
    ds:denominazione ?title.


  OPTIONAL { ?obj ds:tipologia ?category }
  OPTIONAL { ?obj ds:autore ?creator }
  OPTIONAL { ?obj ds:epoca ?period }
  OPTIONAL { ?obj ds:abstract ?descr }
  OPTIONAL { ?obj ds:website ?url }

  OPTIONAL
  {
    ?obj ds:provincia ?prov;
         ds:comune ?city.

    OPTIONAL { ?obj ds:indirizzo ?addressLine }
    OPTIONAL { ?obj ds:cap ?zip }

    BIND ( 'Lombardia' as ?region )
    BIND ( MD5 ( CONCAT ( ?region, ?prov, ?city, ?zip, ?addressLine ) ) AS ?addrHash )
    BIND ( IRI ( CONCAT ( STR ( kr: ), ?addrHash ) ) AS ?addr )

    OPTIONAL {
     ?obj ds:wgs84_x ?lat;
          ds:wgs84_y ?long.

     BIND ( MD5 ( CONCAT ( ?lat, ?long ) ) AS ?locHash )
     BIND ( IRI ( CONCAT ( STR ( kr: ), ?locHash ) ) AS ?loc )
    }
  }
}
